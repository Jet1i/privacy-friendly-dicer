plugins {
    alias(libs.plugins.android.app)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.ksp)
    alias(libs.plugins.ktx.serialization)
}

android {
    compileSdk 34
    namespace 'org.secuso.privacyfriendlydicer'

    defaultConfig {
        applicationId "org.secuso.privacyfriendlydicer"
        minSdkVersion 17
        targetSdkVersion 34
        versionCode 100
        versionName "1.7.4"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            postprocessing {
                removeUnusedCode true
                shrinkResources true
                obfuscate false
                optimizeCode true
            }
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }

    kotlin {
        jvmToolchain(21)
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def appName = "pfa-dicer"
            outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
        }
    }

    lintOptions {
        abortOnError false
    }

    lint {
        lintConfig = file("lint.xml")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation libs.junit
    implementation libs.ax.appcompat
    implementation libs.material
    implementation libs.ax.constraintlayout
    implementation libs.bundles.ax.lifecycle

    implementation(project(":model"))
    implementation(project(":ui-view"))

    implementation(libs.ax.room)
    ksp(libs.ax.room.compiler)
    implementation(libs.ax.room.ktx)

    implementation(libs.ax.work.runtime)
    implementation(libs.ax.work.runtime.ktx)
    androidTestImplementation(libs.ax.work.testing)

    implementation libs.ktx.serialization.json
}
